keys := new Grid "789\n456\n123\nX0A"
arrows := new Grid "X^A\n<v>"
keydown := memo (code: string, robots: number, human = false) =>
  if robots is 0 
    return code#
  pad := human ? keys : arrows
  curr .= pad.find "A"
  count .= 0
  for button of code
    to := pad.find(button)!
    queue := [{ ...curr, push: "" }]
    min .= Infinity
    while queue#
      //@ts-ignore
      { x, y, push } := queue.shift()
      if (isEqualWith {x, y}, pad.find "X") continue
      if (isEqualWith {x, y}, to)
        min = Math.min min, keydown `${push}A`, human ? robots : robots - 1
      if (to.x > x) queue.push { x: x + 1, y, push: `${push}>` }
      if (to.x < x) queue.push { x: x - 1, y, push: `${push}<` }
      if (to.y > y) queue.push { x, y: y + 1, push: `${push}v` }
      if (to.y < y) queue.push { x, y: y - 1, push: `${push}^` }
    count += min
    curr = to
  count

log for sum code of getLines input 
  int(code) * keydown code, 2, true

log for sum code of getLines input 
  int(code) * keydown code, 25, true
